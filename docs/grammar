# basic example of syntax

expression -> literal
	| unary
	| binary
	| grouping
	;

literal -> NUMBER | STRING | "true" | "false" | "null" ;

unary -> ( "-" | "!" ) expression ;

binary -> expression operator expression ;

grouping -> "(" expression ")" ;

operator -> "+" | "-" | "*" | "/" | "%"
	| "==" | "!=" | ">" | "<" | ">=" | "<=" ;

-------------------------------------------------------------------------------

program -> delcaration* EOF ;

declaration -> varDecl | constDecl | statement;

varDecl -> "var" IDENTIFIER ( "=" expression )? ";" ;

constDecl -> "const" IDENTIFIER "=" expression ";" ;

statement -> exprStmt
	| printStmt
	| ifStmt
	| whileStmt
	| forStmt
	| block
	| breakStmt
	| continueStmt
	| returnStmt
	;

exprStmt -> expression ";" ;

printStmt -> "print" expression ";" ;

ifStmt -> "if" "(" expression ")" statement ( "else" statement )? ;

whileStmt -> "while" "(" expression ")" statement ;

forStmt -> "for" "(" ( varDecl | exprStmt | ";") expression? ";" expression ")" statement ;

block -> "{" delcaration* "}" ;

breakStmt -> "break" ";" ;

continueStmt -> "continue" ";" ;

returnStmt -> "return" expression? ";" ;

expression -> assignment ;

assignment -> IDENTIFIER ( "=" | "+=" | "-=" | "*=" | "/=" | "%=" ) expression | ternary ;

ternary -> or ( "?" expression ":" expression )? ;

or -> and ( "||" or )* ;

and -> equality ( "&&" and )* ;

equality -> equality ( ("==" | "!=" ) comparison )* | comparison ;

comparison -> comparison ( ( ">" | ">=" | "<" | "<=" ) addition )* | addition ;

addition -> addition ( ( "-" "+" ) multiplication )* | multiplication ;

multiplication -> unary ( ( "*" | "/" | "%" ) unary )* ;

unary -> ( "!" | "-" ) unary | prefix ;

prefix -> ( "++" | "--" ) IDENTIFIER | postfix ;

postfix -> IDENTIFIER ( "++" | "--" ) | call ;

call -> primary ( "(" arguments? ")" )* ;

arguments -> expression ( "," expression )* ;

primary -> NUMBER | STRING | IDENTIFIER | function | "true" | "false" | "null" | "(" expression ")" ;

function -> "function" "(" parameters? ")" block
	| "(" parameters ")" "=>" statement
	;

parameters -> IDENTIFIER ( "," IDENTIFIER )* ;

-------------------------------------------------------------------------------

